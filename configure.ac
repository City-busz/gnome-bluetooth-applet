AC_PREREQ(2.52)

m4_define(gnomebt_version_major, 2)
m4_define(gnomebt_version_minor, 27)
m4_define(gnomebt_version_micro, 7.2)

AC_INIT([gnome-bluetooth],
        [gnomebt_version_major.gnomebt_version_minor.gnomebt_version_micro],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=gnome-bluetooth])

AC_CONFIG_SRCDIR([applet/main.c])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([1.9 dist-bzip2 no-dist-gzip check-news])

# Before making a release, the GNOMEBT_LT_VERSION string should be modified.
# The string is of the form C:R:A.
# - If interfaces have been changed or added, but binary compatibility has
#   been preserved, change to C+1:0:A+1
# - If binary compatibility has been broken (eg removed or changed interfaces)
#   change to C+1:0:0
# - If the interface is the same as the previous version, change to C:R+1:A
GNOMEBT_LT_VERSION=6:2:0
AC_SUBST(GNOMEBT_LT_VERSION)

AM_MAINTAINER_MODE

AC_LANG_C
AC_PROG_CC
AC_PROG_INSTALL

dnl Initialize libtool
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

GETTEXT_PACKAGE=gnome-bluetooth2
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",
					[The name of the gettext domain])

AM_GLIB_GNU_GETTEXT
IT_PROG_INTLTOOL([0.35.0])

AC_PATH_PROG([XMLLINT],[xmllint])
AC_PATH_PROG(GCONFTOOL, [gconftool-2])
AM_GCONF_SOURCE_2

dnl gtk-doc checks
GTK_DOC_CHECK(1.9)

AC_DEFINE_UNQUOTED(LOCALEDIR, "${prefix}/share/locale", [Directory for the localization files])

AC_ARG_ENABLE(desktop-update, AC_HELP_STRING([--disable-desktop-update],
					[Disable desktop database update]))
if (test "$enable_desktop_update" != no); then
	AC_PATH_PROG(UPDATE_DESKTOP_DATABASE, [update-desktop-database])
fi
AM_CONDITIONAL([DESKTOP_UPDATE], [test -n "$UPDATE_DESKTOP_DATABASE"])

AC_ARG_ENABLE(icon-update, AC_HELP_STRING([--disable-icon-update],
					[Disable icon cache update]))
if (test "$enable_icon_update" != no); then
	AC_PATH_PROG(UPDATE_ICON_CACHE, [gtk-update-icon-cache])
fi
AM_CONDITIONAL([ICON_UPDATE], [test -n "$UPDATE_ICON_CACHE"])

GTK_REQUIRED=2.15.0
DBUS_GLIB_REQUIRED=0.74
HAL_REQUIRED=0.5.8
NOTIFY_REQUIRED=0.4.3

dnl Requires for the properties window
PKG_CHECK_MODULES(PROPS,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gconf-2.0
		  gtk+-2.0
		  unique-1.0)

dnl Requires for the applet
PKG_CHECK_MODULES(APPLET,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gconf-2.0
		  gmodule-export-2.0
		  gtk+-2.0 >= $GTK_REQUIRED
		  libnotify >= $NOTIFY_REQUIRED
		  unique-1.0)

dnl Requires for the sendto app
PKG_CHECK_MODULES(SENDTO,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gtk+-2.0
		  gio-2.0)

dnl Requires for the wizard dialogue
PKG_CHECK_MODULES(WIZARD,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gmodule-export-2.0
		  gtk+-2.0
		  unique-1.0)

dnl Requires for the public library
PKG_CHECK_MODULES(LIBGNOMEBT,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gtk+-2.0)

dnl Requires for the private library
PKG_CHECK_MODULES(COMMON,
		  dbus-glib-1 >= $DBUS_GLIB_REQUIRED
		  gmodule-2.0
		  gtk+-2.0
		  hal >= $HAL_REQUIRED)

dnl Requires for the geoclue plugin
PKG_CHECK_MODULES(GEOCLUE,
		  gconf-2.0
		  dbus-glib-1)

dnl Requires for the plugins
PKG_CHECK_MODULES(PLUGINS,
		  gtk+-2.0)

DBUS_BINDING_TOOL="dbus-binding-tool"
AC_SUBST(DBUS_BINDING_TOOL)

GLIB_GENMARSHAL=`$PKG_CONFIG --variable=glib_genmarshal glib-2.0`
AC_SUBST(GLIB_GENMARSHAL)

GOBJECT_INTROSPECTION_CHECK([0.6.3])

GNOME_COMMON_INIT
GNOME_DEBUG_CHECK
GNOME_COMPILE_WARNINGS([maximum])
GNOME_CXX_WARNINGS
GNOME_MAINTAINER_MODE_DEFINES

GNOME_DOC_INIT

AC_OUTPUT(Makefile
	  gnome-bluetooth-1.0.pc
	  icons/Makefile
	  lib/Makefile
	  lib/plugins/Makefile
	  applet/Makefile
	  applet/bluetooth-applet.desktop.in
	  properties/Makefile
	  properties/bluetooth-properties.desktop.in
	  wizard/Makefile
	  sendto/Makefile
	  docs/Makefile
	  docs/reference/Makefile
	  docs/reference/libgnome-bluetooth/Makefile
	  docs/reference/libgnome-bluetooth/version.xml
	  po/Makefile.in
	  help/Makefile
)

